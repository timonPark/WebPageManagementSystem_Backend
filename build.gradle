buildscript {
    ext {
        springBootVersion = '3.2.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.park'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = '17'
    targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.17.0'
        implementation group: 'com.github.loki4j', name: 'loki-logback-appender', version: '1.5.1'
        implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
        runtimeOnly ("io.micrometer:micrometer-registry-prometheus")
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mockito:mockito-core:4.2.0'

    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':wpms-core') {
    // 공통 코드

    bootJar { enabled = false } // core 은 bootJar 로 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
    }
}

project(':wpms-api-server') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':wpms-core')
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        implementation("org.postgresql:postgresql:42.7.3")
        implementation("org.springframework.boot:spring-boot-starter-web")

        implementation("org.postgresql:postgresql:42.7.3")
        implementation "com.netflix.graphql.dgs:graphql-dgs-spring-boot-starter:7.6.0"
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    }
}



